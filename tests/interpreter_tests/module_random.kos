#
# Copyright (c) 2014-2020 Chris Dragan
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to
# deal in the Software without restriction, including without limitation the
# rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
# sell copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
# IN THE SOFTWARE.
#

import random

fun expect_fail(function)
{
    var failed = false
    try {
        function()
    }
    catch const e {
        failed = true
    }
    assert failed
}

# random.rand_integer
do {
    var sum = 0
    for var i = 0; i < 0x100; i += 1 {
        sum += random.rand_integer() >> 13
    }

    sum >>= 59

    assert sum >= -0x10
    assert sum <  0x10

    for var i = 0; i < 10; i += 1 {
        const value = random.rand_integer(-9, 50)
        assert value >= -9
        assert value <= 50
    }

    do {
        const v = random.rand_integer(0, 1)
        assert v == 0 || v == 1
    }

    assert random.rand_integer(1, 1) == 1
    assert random.rand_integer(-10000, -10000) == -10000

    expect_fail(()=>random.rand_integer(1))
    expect_fail(()=>random.rand_integer(1, 0))
}

# random.rand_float
do {
    for var i = 0; i < 0x100; i += 1 {
        const r = random.rand_float()
        assert r >= 0
        assert r < 1
    }
}

# random.random.prototype.integer
do {
    expect_fail(()=>random.random.prototype.integer.apply(void, []))
    expect_fail(()=>random.random.prototype.integer.apply({}, []))
}

# random.seed
do {
    expect_fail(()=>random.random(void))
    expect_fail(()=>random.random(10e19))

    const seed = random.rand_integer()

    const a = []

    do {
        const rand = random.random(seed)

        a.resize(256)

        for var i = 0; i < 256; i += 1 {
            a[i] = rand.integer()
        }
    }

    do {
        const rand = random.random(seed)

        for var i = 0; i < 256; i += 1 {
            assert a[i] == rand.integer()
        }
    }
}
