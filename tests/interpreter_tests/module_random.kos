# SPDX-License-Identifier: MIT
# Copyright (c) 2014-2021 Chris Dragan

import random

fun expect_fail(function)
{
    var failed = false
    try {
        function()
    }
    catch const e {
        failed = true
    }
    assert failed
}

# random.rand_integer
do {
    var sum = 0
    for var i = 0; i < 0x100; i += 1 {
        sum += random.rand_integer() >> 13
    }

    sum >>= 59

    assert sum >= -0x10
    assert sum <  0x10

    for var i = 0; i < 10; i += 1 {
        const value = random.rand_integer(-9, 50)
        assert value >= -9
        assert value <= 50
    }

    do {
        const v = random.rand_integer(0, 1)
        assert v == 0 || v == 1
    }

    assert random.rand_integer(1, 1) == 1
    assert random.rand_integer(-10000, -10000) == -10000

    expect_fail(()=>random.rand_integer(1))
    expect_fail(()=>random.rand_integer(min = 1))
    expect_fail(()=>random.rand_integer(max = 1))
    expect_fail(()=>random.rand_integer(1, 0))
}

# random.rand_float
do {
    for var i = 0; i < 0x100; i += 1 {
        const r = random.rand_float()
        assert r >= 0
        assert r < 1
    }
}

# random.random.prototype.integer
do {
    expect_fail(()=>random.random.prototype.integer.apply(void, []))
    expect_fail(()=>random.random.prototype.integer.apply({}, []))
}

# random.seed
do {
    expect_fail(()=>random.random(true))
    expect_fail(()=>random.random(10e19))

    const seed = random.rand_integer()

    const a = []

    do {
        const rand = random.random(seed)

        a.resize(256)

        for var i = 0; i < 256; i += 1 {
            a[i] = rand.integer()
        }
    }

    do {
        const rand = random.random(seed)

        for var i = 0; i < 256; i += 1 {
            assert a[i] == rand.integer()
        }
    }
}
