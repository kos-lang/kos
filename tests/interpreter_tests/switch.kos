do {
    fun test(a)
    {
        switch a {

            case void  { return 1 }

            case true  { return 2 } fallthrough

            case false { return 3 }

            case "x"   { return 4 }

            case 89    { return 5 }
        }
        return void
    }

    assert test(void)  == 1
    assert test(2>1)   == 2
    assert test(2<1)   == 3
    assert test("x")   == 4
    assert test(89)    == 5
    assert test("")    == void
    assert test(12)    == void
    assert test([])    == void
}

do {
    var value = void

    fun mutate { value = 123 }

    switch mutate() { }

    assert value == 123
}

do {
    fun test(x)
    {
        switch x {
            case 5 { return true }
        }
        return false
    }

    for var i = 0; i < 10; i += 1 {
        assert (i == 5) == test(i)
    }
}

do {
    var a = 1

    switch 8 {
        else { a = 2 }
    }

    assert a == 2
}

do {
    fun test(a)
    {
        var ret = ""

        switch a + 1 {

            case 1 {
                ret += "a"
            }

            case 2 {
                ret += "b"
            }
            fallthrough

            case 3 {
                ret += "c"
            }

            else {
                ret += "d"
            }

            case 4 fallthrough
            case 5 fallthrough

            case 6 {
                ret += "e"
            }
        }

        return ret
    }

    assert test(0) == "a"
    assert test(1) == "bc"
    assert test(2) == "c"
    assert test(3) == "e"
    assert test(4) == "e"
    assert test(5) == "e"
    assert test(6) == "d"
    assert test(7) == "d"
}

do {
    var sum = 0
    for var i = 1; i < 10; i += 1 {
        switch i {
            case 2 fallthrough
            case 4 fallthrough
            case 5 { continue }

            else   { sum = sum * 10 + i }

            case 8 { break }
        }
    }

    assert sum == 1367
}

do {
    fun select(a)
    {
        const x = 1
        const y = 2
        switch (a) {
            case x { return 7 }
            case y { return 14 }
        }
        return 0
    }

    assert select(1) == 7
    assert select(2) == 14
    assert select(3) == 0
}

do {
    var x = 2
    fun select(a)
    {
        switch a + 1 {
            case 0 fallthrough
            case 1 fallthrough
            case 2 { return a }
            case x * 2 { return 10 }
        }
    }

    assert select(-2) == void
    assert select(-1) == -1
    assert select(0)  == 0
    assert select(1)  == 1
    assert select(2)  == void
    assert select(3)  == 10
    assert select(4)  == void
}
