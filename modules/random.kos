#
# Copyright (c) 2014-2020 Chris Dragan
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to
# deal in the Software without restriction, including without limitation the
# rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
# sell copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
# IN THE SOFTWARE.
#

const rand = random()

/* @item random rand_integer()
 *
 *     rand_integer()
 *     rand_integer(min, max)
 *
 * Generates a pseudo-random integer with uniform distribution.
 *
 * Returns a random integer.
 *
 * The first variant generates any integer number.
 *
 * The second variant generates an integer between the chosen `min` and `max`
 * values.  The `min` and `max` values are included in the possible range.
 *
 * Examples:
 *
 *     > random.rand_integer()
 *     -3655836363997440814
 *     > random.rand_integer(-100, 100)
 *     42
 */

public fun rand_integer(args...)
{
    return rand.integer.apply(rand, args)
}

/* @item random rand_float()
 *
 *     rand_float()
 *
 * Generates a pseudo-random float with uniform distribution from 0.0
 * (inclusive) to 1.0 (exclusive).
 *
 * Returns a float in the range from 0.0 to 1.0, where 0.0 can be possibly
 * produced and 1.0 is never produced.
 *
 * Example:
 *
 *     > random.rand_float()
 *     0.05080192760294
 */
public fun rand_float
{
    return rand.float.apply(rand, [])
}

/* @item random random.prototype.shuffle()
 *
 *     random.prototype.shuffle(iterable)
 *
 * Generates a pseudo-random permutation of elements from an iterable object.
 *
 * The pseudo-random number generator object `this` is used to generate
 * the permutation.
 *
 * Returns an array containing a permutation of the elements extracted
 * from the `iterable` using its `iterator()` function.
 *
 * Example:
 *
 *     > const r = random.random(42)
 *     > range(10) -> r.shuffle
 *     [7, 4, 6, 5, 2, 0, 1, 3, 8, 9]
 */
random.prototype.shuffle = fun(iterable) {
    const elements = [ iterable ... ]
    if elements.size > 1 {
        const max = elements.size - 1
        var   idx = max
        while idx {
            idx -= 1
            const idx2 = this.integer(0, max)

            if idx != idx2 {
                const tmp = elements[idx]
                elements[idx] = elements[idx2]
                elements[idx2] = tmp
            }
        }
    }
    return elements
}

/* @item random shuffle()
 *
 *     shuffle(iterable)
 *
 * Generates a pseudo-random permutation of elements from an iterable object.
 *
 * Returns an array containing a permutation of the elements extracted
 * from the `iterable` using its `iterator()` function.
 *
 * Example:
 *
 *     > range(10) -> shuffle
 *     [9, 4, 3, 0, 5, 7, 1, 6, 8, 2]
 */
public fun shuffle(iterable)
{
    return random().shuffle(iterable)
}

# TODO: sample, uniform float range, normal distribution
